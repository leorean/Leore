<!--This Document is generated by GameMaker, if you edit it by hand then you do so at your own risk!-->
<object>
  <spriteName>mskPlayer</spriteName>
  <solid>0</solid>
  <visible>-1</visible>
  <depth>-10</depth>
  <persistent>0</persistent>
  <parentName>objLiving</parentName>
  <maskName>&lt;undefined&gt;</maskName>
  <events>
    <event eventtype="0" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>instance_create(x, y, objCamera);

cx = (bbox_right + bbox_left) / 2;
cy = (bbox_bottom + bbox_top) / 2 - 2;

zVel = 0;
zGrav = .15;
z = 0;

xVel = 0;
yVel = 0;
maxVel = 6;
xPrevDrown = x;
yPrevDrown = y;
xPrevHole = x;
yPrevHole = y;
spd = 1.5;

inWater = false;
inHole = false;
isRolling = false;
inShallowWater = false;
inWater = false;
inCliff = false;

fallTimeout = 0;
maxFallTimeout = 60;
maxCliffTimer = 30;
cliffTimer = 0;

state = S.IDLE;
fCur = 0;
frame = 0;
dir = global.dir;
angle = 270;
row = 0;

sword = instance_create(x, y, objSword);
hp = 3;
hurt = 0;
invincible = 0;

hole = noone;

canDestroy = false;
dsCol = ds_list_create();
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="1" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>ds_list_destroy(dsCol);
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="3" enumb="2">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>cx = (bbox_right + bbox_left) / 2;
cy = (bbox_bottom + bbox_top) / 2;
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="3" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>if (global.state != G.PLAY) exit;

// FLAGS
inShallowWater = z == 0 &amp;&amp; tile_layer_find(LAYER_WATER, x, y);
inWater = false;
if (inShallowWater) {
    var wt = tile_layer_find(LAYER_WATER, x, y);
    var wt_index = getTileIndexForCoordinates(tile_get_left(wt), tile_get_top(wt));
    if (isWaterTile(wt_index)) {
        inWater = true;
        inShallowWater = false;
    }
}

if (state == S.FALL) {
    xVel = 0;
    yVel = 0;
    if (fallTimeout) {
        dir = D.DOWN;
        fallTimeout = max(fallTimeout - 1, 0);
    }
    else {
        x = xPrevHole;
        y = yPrevHole;
        hurt = 0; invincible = 0;
        state = S.IDLE;
    }
} else {
    inHole = false;
}

if (inWater) {
    if (!global.swim &amp;&amp; state != S.DROWN) {
        state = S.DROWN;
        fCur = 0;
    }
    if (global.swim) {
        state = S.SWIM;
    }
}

if (state == S.DROWN) {
    xVel *= .9;
    yVel *= .9;
}

// STORING PREV COORDINATES
if (!inShallowWater &amp;&amp; !inWater) {
    xPrevDrown = x;
    yPrevDrown = y;
}

var hcp = instance_place(x, y, objHoleCheckPoint);
if (hcp) {
    xPrevHole = hcp.x + 8;
    yPrevHole = hcp.y + 8;    
}

// INPUT HANDLING
if (inWater &amp;&amp; !global.swim) setControlsEnabled(false);
if (inHole) setControlsEnabled(false);
if (hurt) setControlsEnabled(false);

kUp = keyPressed("up");
kDown = keyPressed("down");
kLeft = keyPressed("left");
kRight = keyPressed("right");
kAction1 = keyPressed("action1");
kAction2 = keyPressed("action2");
kAction1Pressed = keyPressed("action1pressed");
kAction2Pressed = keyPressed("action2pressed");

if (kLeft &amp;&amp; kRight) { kLeft = 0; kRight = 0; }
if (kUp &amp;&amp; kDown) { kUp = 0; kDown = 0; }

// TRAP FLOOR
var tf = instance_place(x, y, objTrapFloor);
if (tf &amp;&amp; !tf.activated) {
    tf.activated = true;
}

// CLIFFS
if (!inCliff)
    cliff = instance_place(x, y + 2, objCliff);

if (cliff != noone) {
    if (dir == D.DOWN &amp;&amp; kDown) {
        cliffTimer = min(cliffTimer + 1, maxCliffTimer);
    } else {
        cliffTimer = 0;
    }
    
    if (cliffTimer == maxCliffTimer) {
        inCliff = true;
        z = -1;
        zVel = -2.5;
        cliffTimer = 0;
    }
} else {
    cliffTimer = 0;
}

if (inCliff) {
    y += 1.5;
    if (!place_meeting(x, y, objSolid)) {
        inCliff = false;
    }
    if (z == 0) {
        inCliff = false;
    }
}

// ITEMS
var item = instance_place(x, y, objItem);
if (item) {
    if (state != S.ACQUIRE) {
        dir = D.DOWN;
        state = S.ACQUIRE;
    }
    item.acquired = true;
}

if (state == S.ACQUIRE) {
    xVel = 0;
    yVel = 0;
}

// TOUCH SIGNS
var sgn = instance_place(x, y, objSign);
if (sgn != noone &amp;&amp; dir == D.UP) {
    if (kAction1Pressed)
        showMessage(sgn.text);
}

// DOORS
if (state == S.WALK || state == S.ROLL) {
    var door = instance_place(x, y, objDoor);
    if (door) {
        if (dir == door.dir) {
            with(objLevelTransition) instance_destroy();
            var trans = instance_create(door.x + 8, door.y + 8, objLevelTransition);
            trans.tx = door.tx;
            trans.ty = door.ty;
            trans.target = door.target;
            switch(door.targetdir) {
                case "up":
                    global.dir = D.UP;
                break;
                case "down":
                    global.dir = D.DOWN;
                break;
                case "left":
                    global.dir = D.LEFT;
                break;
                case "right":
                    global.dir = D.RIGHT;
                break;
            }
        }
    }
}

// WALKING
if (state == S.IDLE || state == S.WALK) {

    if (kLeft) dir = D.LEFT;
    if (kRight) dir = D.RIGHT;
    if (kUp) dir = D.UP;
    if (kDown) dir = D.DOWN;
    
    if (kLeft) {
        xVel = max(xVel - .2, -spd);
    }
    if (kRight) {
        xVel = min(xVel + .2, spd);
    }
    if (kUp) {
        yVel = max(yVel - .2, -spd);
    }
    if (kDown) {
        yVel = min(yVel + .2, spd);
    }
    
    if (!kLeft &amp;&amp; !kRight) xVel = 0;
    if (!kUp &amp;&amp; !kDown) yVel = 0;
        
    if (state == S.IDLE) {    
        if (kLeft || kRight || kUp || kDown) state = S.WALK;
    }
    if (state == S.WALK)
        if (!kLeft &amp;&amp; !kRight &amp;&amp; !kUp &amp;&amp; !kDown) state = S.IDLE;

    if (global.action == A.ROLL) {
        if (kAction1) state = S.ROLL;
    }
        
} 

// ROLLING
if (state == S.ROLL) {
    
    if (kLeft) dir = D.LEFT;
    if (kRight) dir = D.RIGHT;
    if (kUp) dir = D.UP;
    if (kDown) dir = D.DOWN;

    if (kAction1) {
        if (kRight) { if (abs(xVel) &lt; 3) xVel += .1; }
        if (kLeft) {  if (abs(xVel) &lt; 3) xVel -= .1; }    
        if (kDown) { if (abs(yVel) &lt; 3) yVel += .1; }
        if (kUp) {   if (abs(yVel) &lt; 3) yVel -= .1; }
    }
    if (z == 0) {        
        var spring = collision_point(x, y, objSpring, false, true);
        if (spring &amp;&amp; !spring.sprung) {
            spring.sprung = true;
            zVel = -3;
            xVel = sign(xVel) * min(abs(xVel), 2);
            yVel = sign(yVel) * min(abs(yVel), 2);
        }
    }
    if (!kAction1 &amp;&amp; xVel == 0 &amp;&amp; yVel == 0 &amp;&amp; z == 0) {
        zVel = 0;
        state = S.IDLE;
    }
    
    if ((!kLeft &amp;&amp; !kRight) || !kAction1) xVel = sign(xVel)*max(abs(xVel) - .1, 0);
    if (   (!kUp &amp;&amp; !kDown) || !kAction1) yVel = sign(yVel)*max(abs(yVel) - .1, 0);

}

if (inShallowWater &amp;&amp; !inWater) {
    xVel = sign(xVel) * min(abs(xVel), 1);
    yVel = sign(yVel) * min(abs(yVel), 1);
}

// SWIMMING
if (state == S.SWIM) {
    if (!inWater) {
        state = S.IDLE;
    }
    
    if (kLeft) dir = D.LEFT;
    if (kRight) dir = D.RIGHT;
    if (kUp) dir = D.UP;
    if (kDown) dir = D.DOWN;

    if (kRight) { xVel += .05; }
    if (kLeft) {  xVel -= .05; }
    
    if (!kLeft &amp;&amp; !kRight) xVel = sign(xVel) * max(abs(xVel) - .05, 0);
    if (!kUp &amp;&amp; !kDown)    yVel = sign(yVel) * max(abs(yVel) - .05, 0);
    
    if (kDown) {  yVel += .05; }
    if (kUp) {    yVel -= .05; }
    
    xVel = sign(xVel) * min(abs(xVel), 1);
    yVel = sign(yVel) * min(abs(yVel), 1);

}

// DESTROYING OBJECTS (ROLL)
canDestroy = state == S.ROLL /*&amp;&amp; (max(abs(xVel), abs(yVel)) &gt; .5)*/ &amp;&amp; z == 0;

ds_list_clear(dsCol);
instance_deactivate_object(objDestroyable);
instance_activate_region(x - 32, y - 32, 64, 64, true);
for(var i = 0; i &lt; instance_number(objDestroyable); i++) {

    var d = instance_find(objDestroyable, i);
    if (place_meeting(x + xVel, y + yVel, d)) {
        if (!ds_list_find_index(dsCol, d)) {
            if (canDestroy) {
                d.hp = max(d.hp - 1, 0);
            }
            if (d.hp == 0) {
                ds_list_add(dsCol, d);
            }
        }
    }
}
instance_activate_object(objDestroyable);

// ROLLING AGAINST ENEMIES
if (canDestroy) {
    instance_deactivate_object(objEnemy);
    instance_activate_region(x - 32, y - 32, 64, 64, true);
    for(var i = 0; i &lt; instance_number(objEnemy); i++) {
        var e = instance_place(x + xVel, y + yVel, objEnemy);
        if (e &amp;&amp; e.hp &gt; 0 &amp;&amp; is(e, objEnemyBall)) {
            var angle = point_direction(x, y, e.x, e.y);

            var vel = max(abs(xVel), abs(yVel), 1.5);
                
            e.xVel = lengthdir_x(vel, angle);
            e.yVel = lengthdir_y(vel, angle);
            
            xVel = -lengthdir_x(1.5, angle);
            yVel = -lengthdir_y(1.5, angle);
            
            e.hurt = 20;
        }
    }
    instance_activate_object(objEnemy);
}

// GETTING HIT
invincible = max(invincible - 1, 0);
var enemy = instance_place(x, y, objEnemy);
if (enemy &amp;&amp; enemy.hp &gt; 0 &amp;&amp; !enemy.hurt 
    &amp;&amp; !hurt 
    &amp;&amp; !invincible
    &amp;&amp; state != S.FALL
    &amp;&amp; state != S.DROWN) {
    var kbAngle = point_direction(cx, cy, enemy.x, enemy.y);
    xVel = lengthdir_x(-3, kbAngle);
    yVel = lengthdir_y(-3, kbAngle);
    hurt = 20;
    state = S.HURT;
}

if (state == S.HURT) {
    hurt = max(hurt - 1, 0);
    xVel *= .85;
    yVel *= .85;
    
    if (!hurt) {
        state = S.IDLE;
        invincible = 45;
    }
}

// HOLES
if (z == 0) {
    var minDist = 999;
    hole = noone;
    instance_deactivate_object(objHole);
    instance_activate_region(cx - 2*TILE, cy - 2*TILE, 4*TILE, 4*TILE, true);

    for (var i = 0; i &lt; instance_number(objHole); i++) {
        var h = instance_find(objHole, i);
        if (place_meeting(x, y, h)) {
            var dist = point_distance(x, y, h.x + 8, h.y + 8);
            if (dist &lt; minDist) {
                minDist = dist;
                hole = h;
            }
        }
    }

    instance_activate_object(objHole);
    if (instance_exists(hole)) {
            var ang = point_direction(cx, cy, hole.x + 8, hole.y + 8);
            var hole_dist = point_distance(cx, cy, hole.x + 8, hole.y + 8);
    
            xVel += lengthdir_x(.3, ang);
            yVel += lengthdir_y(.3, ang);

        if (state != S.FALL) {

            if (collision_point(cx, cy, objHole, 0, 1)) {
                inHole = true;
                hole = noone;
                state = S.FALL;
                fallTimeout = maxFallTimeout;
            }
        }
    }
}

// SWORD
if (state == S.ATTACK) {
    xVel = 0;
    yVel = 0;
}
if (state == S.IDLE || state == S.WALK) {
    if (kAction2Pressed &amp;&amp; z == 0) {
        if (global.sword &gt; -1) {
            fCur = 0;
            state = S.ATTACK;
            sword.state = S.ATTACK;
        }
    }
}

// Z-MOVEMENT

zVel += zGrav;
z += zVel;

if (z + zVel &gt;= 0) {
    zVel = 0;
}
z = min(z, 0);
if (z != 0) { rollTimout = 5; }

// COLLISION 

var xCol = instance_place(x + xVel, y, objDestroyable);
var yCol = instance_place(x, y + yVel, objDestroyable);
if (xCol &amp;&amp; is(xCol, objEnemy)) xCol = noone;
if (yCol &amp;&amp; is(yCol, objEnemy)) yCol = noone;

// STAIRS
var stairs = instance_place(x + xVel, y + yVel, objStairs);
if (stairs &amp;&amp; z == 0) {
    yVel = sign(yVel) * min(abs(yVel), .8);
}

instance_deactivate_object(objSolid);
instance_activate_region(cx - 32, cy - 32, 64, 64, true);
xCol = false;
yCol = false;
for (var i = 0; i &lt; instance_number(objSolid); i++) {
    var s = instance_find(objSolid, i);
    if (!is(s, objHole) 
        &amp;&amp; !is(s, objStairs)
        &amp;&amp; ds_list_find_index(dsCol, s) == -1) {            
        if (place_meeting(x + xVel, y, s)) {
            xCol = true;
        }
        if (place_meeting(x, y + yVel, s)) {
            yCol = true;
        }
    }
}
instance_activate_object(objSolid);

if (!xCol) {
    x += xVel;
} else {
    if (state == S.ROLL &amp;&amp; z == 0) {
        xVel = -xVel * .5;
        if (abs(xVel) &gt; .5) {
            xVel *= .8;
            //zVel = -1.2;
        }
    }
    else
        xVel = 0;
}
if (!yCol) {
    y += yVel;
} else {
    if (state == S.ROLL &amp;&amp; z == 0) {
        yVel = -yVel * .5;
        if (abs(yVel) &gt; .5) {
            yVel *= .8;
            //zVel = -1.2;
        }
    }
    else
        yVel = 0;
}
for(var i = 0; i &lt; 360; i++)
    move_outside_solid(i, 1);

x = max(min(x, room_width - 8), 8);
y = max(min(y, room_height - 8), 8);
instance_activate_object(objDestroyable);
instance_activate_object(objStairs);
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="8" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>// DRAWING &lt;-&gt; STATE LOGIC
if (global.state == G.PLAY
    || state == S.ACQUIRE) {
    switch(state) {
        case S.IDLE:
            fCur = (fCur + .1) % 4;
            frame = floor(fCur);
        break;
        case S.WALK:
            fCur = (fCur + .2) % 4;
            frame = 4 + floor(fCur);
        break;
        case S.ROLL:
            fCur = (fCur + max(.2, .6 * sqrt(abs(xVel) + abs(yVel)) / 3 )) % 4;
            frame = 8 + floor(fCur);
        break;
        case S.FALL:
            frame = 0;
        break;
        case S.ATTACK:
            fCur = 0;
            frame = 12 + floor(fCur);        
        break;
        case S.DROWN:
            fCur = min(fCur + .1, 4);
            frame = floor(fCur);
            if (fCur == 4) {
                if (inWater) {
                    x = xPrevDrown;
                    y = yPrevDrown;
                } else {
                    state = S.IDLE;
                }
            }
        break;
        case S.SWIM:
            fCur = (fCur + .1) % 3;
            frame = 13 + floor(fCur);        
        break;
        case S.ACQUIRE:
            fCur = 0;
            frame = 16;
        break;
    }
}

switch(dir) {
    case D.UP:
        row = 1;
    break;
    case D.DOWN:
        row = 0;
    break;
    case D.LEFT:
        row = 3;
    break;
    case D.RIGHT:
        row = 2;
    break;
}

// DRAWING 

// shadow
if (!inShallowWater &amp;&amp; !inWater &amp;&amp; !inHole)
{
    draw_sprite(sprShadow, -1, x, bbox_bottom);
}

if (!inHole &amp;&amp; state != S.DROWN) {
    if (((invincible + hurt) % 4) &lt; 2) {
    } else {
        d3d_set_fog(1, c_black, 0, 0)   
    }
    draw_sprite_part(sprPlayer, -1, frame*32, row * 32, 32, 32, x - 16, cy - 18 + z);
    d3d_set_fog(0,0,0,0)
}
else {
    var s_fall = (fallTimeout / maxFallTimeout);
    draw_sprite_ext(sprPlayerFall, -1, cx, cy, s_fall, s_fall, s_fall * 360, c_white, s_fall);
}

// shallow water
if (inShallowWater &amp;&amp; !inWater) {
    draw_sprite_part(sprWaterShadow, -1, 32*floor((global.timer / 5) % 4), 0, 32, 32, x - 16, y - 20);
}

if (state == S.DROWN) {
    draw_sprite(sprDrown, frame, cx, cy);
}

if (global.debug) {
    draw_rectangle(bbox_left, bbox_top, bbox_right, bbox_bottom, true);
    draw_circle(cx, cy, 6, 1);
}
</string>
          </argument>
        </arguments>
      </action>
    </event>
  </events>
  <PhysicsObject>0</PhysicsObject>
  <PhysicsObjectSensor>0</PhysicsObjectSensor>
  <PhysicsObjectShape>0</PhysicsObjectShape>
  <PhysicsObjectDensity>0.5</PhysicsObjectDensity>
  <PhysicsObjectRestitution>0.100000001490116</PhysicsObjectRestitution>
  <PhysicsObjectGroup>0</PhysicsObjectGroup>
  <PhysicsObjectLinearDamping>0.100000001490116</PhysicsObjectLinearDamping>
  <PhysicsObjectAngularDamping>0.100000001490116</PhysicsObjectAngularDamping>
  <PhysicsObjectFriction>0.200000002980232</PhysicsObjectFriction>
  <PhysicsObjectAwake>-1</PhysicsObjectAwake>
  <PhysicsObjectKinematic>0</PhysicsObjectKinematic>
  <PhysicsShapePoints/>
</object>
